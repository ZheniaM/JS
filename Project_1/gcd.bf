input:

/////////////
/// first ///
/////////////

(isNaN; 1; first; inp; iscomma; 0)
>+[
    >>,
    >+++ +++[-<--- --- ->]
    +<--[ is num?
        (isNaN; 1; first; num; 0; num?;)
        ----[->+>+<<]
        >-[-<+>]
        >[-[-[-[-[-[-[-[-[-[
            isminus?
            (isNaN; 1; first; num; 0; ^minus?; isNaN?;)
            <<[+]>>
            +++ +++ +++ +++
            >+<[>-]<[
                isNaN
                [-]>+
                <<< <<<[-]+
                >>> >
            ]>>-<
        ]9]8]7]6]5]4]3]2]1]0
        (isNaN; 1; first; num; 0; ^0)
        +++ +++ +++[
            -<<<[->+>+<<]
            >>[-<<+>>]
            >
        ]
        <<[-<+>]
    ]
    (isNaN; 1; first; ^0; iscomma)
    >[ is 44
        -<<<->>>
    ]
    <<<
]

//////////////
/// second ///
//////////////

(isNaN; ^0; first)
>>+[
    >>,
    >+++ +++[-<--- --- ->]
    +<--[ is num?
        (isNaN; 1; first; num; 0; num?;)
        ----[->+>+<<]
        >-[-<+>]
        >[-[-[-[-[-[-[-[-[-[
            isminus?
            (isNaN; first; 1; second; num; 0; ^minus?; isNaN?;)
            <<[+]>>
            +++ +++ +++ +++
            >+<[>-]<[
                isNaN
                [-]>+
                <<< <<< <<[-]+
                >>> >>>
            ]>>-<
        ]9]8]7]6]5]4]3]2]1]0
        (isNaN; 1; first; num; 0; ^0)
        +++ +++ +++[
            -<<<[->+>+<<]
            >>[-<<+>>]
            >
        ]
        <<[-<+>]
    ]
    (isNaN; 1; first; ^0; iscomma)
    >[ is 44
        -<<<->>>
    ]
    <<<
]

////////////////////
/// number check ///
////////////////////

(isNaN; 0; first; ^0; second)
<<<[isNaN
    ->>[-]>>[-]
    (0; 0; 0; 0; ^0) =} (0; 0; ^78; 77; 0) =} (0; 0; 78; ^97; 0)
    +++ +++ +++ ++[-<++++ +++<++++ +++>>]<<+. 11 * 7 plus 1 = 78 = "N"
    >>++++[-<+++ ++>]<. 77 plus 4*5 = 97 = "a"
    [-]<.[-]
    <-<
]>+[isn'tNaN
    (0; ^1; first; 0; second)
    >[<-]>[ first != 0
        [-<<+>>]-
        >>[-<<<+>>>]
        <
    ]<+[ first == 0
        (0; 0; ^0; 0; second; ^if0)
        >+>[<-]>[ second != 0
            [-<< <<+>> >>]
            >
        ]
        <<<-<->
    ]
    (first; second; ^0; is00)
    >[is 0 && 0
        000000000000000000000000000000000000
        (0; 0; 0; ^1)
        +++ ++++[-<+++ +++>]<.
        [-]->
        000000000000000000000000000000000000
    ]<+[

////////////
/// main ///
////////////

        (fnum; snum; ^0) =} (0; 0; 0; fnum; 0; 0; snum)
        -<[->>> >>+<< <<<]
        <[->>>+<<<]
        >>>[
            <[-]
            >>>>[-<<< <<+>> >>>]
            <<<[->>>+>+<<<<]
            >>>>[-<<< <<+>> >>>]
            <<< <<<[
                >->+<[>]
                >[<+>-]
                <<[<]>-
            ]
            >>
        ]
        (0; 0; gcd; ^0; 0; 0; trash)
        >>>[-]
        <<<<[-<<+>>]

        print:
        <<[
            (^gcd; 10copy; 0; 10isempty; 10; quot)
            >>>>+++ +++ +++ +
            <<<<[
                ->+>>+>-
                [<-]<[
                    <<[->>>+<<<]
                    >>>>+<<-<
                ]
                <<
            ]
            (^0; rem; 0; 0; trash; qout)
            >[-<+>]
            ++++ ++++[-<+++ +++>]
            >>>[-]
            >[-<< <<+>> >>]
            <<<<
        ]
        <[.<]
    ]
]